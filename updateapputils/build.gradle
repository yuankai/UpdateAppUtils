apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdk 33
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    namespace = "com.teprinciple.updateapputils"
    lint {
        abortOnError false
    }
    viewBinding {
        enable = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.liulishuo.filedownloader:library:1.7.7'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1'
}

repositories {
    mavenCentral()
}

publishing.repositories {
    maven {
        if (project.hasProperty("nexusUser")) {
            credentials {
                username nexusUser
                password nexusPassword
            }
        }
        name = 'nexus'
        url = "https://nexus.buymice.net/repository/maven-releases/"
    }
}

publishing.publications {
    updateUtils(MavenPublication) {
        groupId = 'net.buymice'
        artifactId = 'android-update-utils'
        version = '2.3.4'
        artifact("$buildDir/outputs/aar/updateapputils-release.aar")
        pom.withXml {

            def dependenciesNode = asNode().appendNode('dependencies')

            configurations.implementation.allDependencies.each {
                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', it.group)
                dependencyNode.appendNode('artifactId', it.name)
                dependencyNode.appendNode('version', it.version)
            }
        }
    }
}