apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // 忽略错误信息
    lintOptions {
        abortOnError false
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.liulishuo.filedownloader:library:1.7.6'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1'
}

repositories {
    mavenCentral()
}

publishing.repositories {
    maven {
        if (project.hasProperty("nexusUser")) {
            credentials {
                username nexusUser
                password nexusPassword
            }
        }
        name = 'nexus'
        url = "http://nexus.dev.buymice.net/repository/maven-releases/"
    }
}

publishing.publications {
    updateUtils(MavenPublication) {
        groupId = 'net.buymice'
        artifactId = 'android-update-utils'
        version = '2.3'
        artifact("$buildDir/outputs/aar/updateapputils-release.aar")
        pom.withXml {

            def dependenciesNode = asNode().appendNode('dependencies')

            configurations.implementation.allDependencies.each {
                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', it.group)
                dependencyNode.appendNode('artifactId', it.name)
                dependencyNode.appendNode('version', it.version)
            }
        }
    }
}